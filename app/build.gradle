plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '1.8.20-1.0.11'
}

android {
    namespace 'site.leos.apps.lespas'
    compileSdk 33

    defaultConfig {
        minSdk 25
        targetSdk 33
        versionCode 87
        versionName '2.8.3'
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "LesPas-v$versionName"
        resValue 'string', 'lespas_version', "v${versionName}"
        resValue 'string', 'sync_authority', "${namespace}.StubProvider"
        resValue 'string', 'file_authority', "${namespace}.fileProvider"
        resValue 'string', 'muzei_authority', "${namespace}.muzei.ArtProvider"

        ksp {
            arg('oom.schemaLocation', "$projectDir/schemas".toString())
            arg('room.incremental', 'true')
        }
    }

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            ext.enableCrashlytics = false
        }
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    aaptOptions {
        noCompress 'tflite'
    }
}

dependencies {
    // TensorFlow
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.2'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.9.0'

    // Core
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.activity:activity-ktx:1.7.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.7'

    // Room components
    def roomVersion = "2.5.1"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Lifecycle components
    def lifecycleVersion = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Kotlin components
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    def coroutineVersion = '1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // Other UI components
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    // Media3
    def media3_version = "1.0.2"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    //implementation "androidx.media3:media3-session:$media3_version"

    // Chris Banes PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    //implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Muzei api
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.1'

    // OSMdroid and OSMbonuspack
    implementation('org.osmdroid:osmdroid-android:6.1.13')  {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation('com.github.MKergall:osmbonuspack:6.9.0') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    // Authentication through system credentials
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'

    // ZXing for blog QR code
    implementation 'com.google.zxing:zxingorg:3.5.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}